# https://dev.twitter.com/rest/public/search
grammar Query

    # Query Shape Rules

    # One or more operators makes up a query
    root        <- __* value:query __*          <Value>
    query       <- (value:operator __*)+        <Values>
    operator    <- or / op

    # Operators can be grouped with an OR operator in pairs.
    # I'm not sure if this is the ideal way to do this with PEG
    # but it works for now.
    or          <- orable or_groups             <Or>
    or_groups   <- (or_sep value:orable)+       <Values>

    # Only some oprators work in an OR
    orable      <- exact
                 / filter
                 / reply
                 / engagement
                 / lang
                 / simple

    # If it's not an OR you can use any operator
    op          <- exact
                 / filter
                 / reply
                 / since
                 / until
                 / list
                 / engagement
                 / lang
                 / exclude
                 / question
                 / negation
                 / simple

    simple      <- hashtag
                 / mention
                 / word

    engagement  <- min_rts
                 / min_faves
                 / min_replies

    # Query Syntax Rules

    exact       <- '"' [^\"]* '"'                               <Including>
    filter      <- k:"filter"           sep v:word              <KV>
    reply       <- k:"to" sep "@"?          v:screen_name       <KV>
    since       <- k:"since"            sep v:date              <KV>
    until       <- k:"until"            sep v:date              <KV>
    list        <- "list"               sep list_name           <List>
    min_rts     <- k:(min "retweets")   sep v:integer           <KV>
    min_faves   <- k:(min "faves")      sep v:integer           <KV>
    min_replies <- k:(min "replies")    sep v:integer           <KV>
    lang        <- k:"lang"             sep v:word              <KV>
    exclude     <- k:"exclude"          sep v:word              <KV>
    question    <- "?"                                          <IsQuestion>
    negation    <- "-" word                                     <Excluding>
    hashtag     <- "#" tag                                      <Including>
    mention     <- "@" screen_name                              <Including>
    word        <- [\S]+                                        <Including>

    # Utility Rules

    or_sep      <- __+ "OR" __+
    list_name   <- screen_name "/" list_slug
    list_slug   <- [a-z-]+
    screen_name <- [a-zA-z0-9_]+
    # There's probably a better way to do this
    date        <- d d d d "-" d d "-" d d
    # This is clearly wrong â€” needs to account for unicode
    tag         <- [a-zA-z0-9_]+
    min         <- "min_"
    sep         <- ":"
    integer     <- d d*
    d           <- [0-9]
    __          <- [\s]
