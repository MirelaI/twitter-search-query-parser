grammar Query

    query       <- __* (op __+)* op __*

    # Operators can be grouped with an OR operator in pairs.
    # I'm not sure if this is the ideal way to do this with PEG
    # but it works for now.
    op          <- operator or operator
                 / operator

    # Order is important here!
    operator    <- exact
                 / filter
                 / reply
                 / since
                 / until
                 / list
                 / min_rts
                 / min_faves
                 / min_replies
                 / lang
                 / question
                 / negation
                 / hashtag
                 / mention
                 / word

    # Operators
    exact       <- '"' [^\"]* '"'
    hashtag     <- "#" tag
    filter      <- "filter" sep word
    reply       <- "to" sep "@"? screen_name
    since       <- "since" sep date
    until       <- "until" sep date
    mention     <- "@" screen_name
    list        <- "list" sep list_name
    min_rts     <- min "retweets" sep integer
    min_faves   <- min "faves" sep integer
    min_replies <- min "replies" sep integer
    lang        <- "lang" sep word
    exclude     <- "exclude" sep word
    negation    <- "-" word
    question    <- "?"
    word        <- [\S]+

    # Utility rules
    or          <- __+ `or` __+
    list_name   <- screen_name "/" list_slug
    list_slug   <- [a-z-]+
    screen_name <- [a-zA-z0-9_]+
    # There's probably a better way to do this
    date        <- d d d d "-" d d "-" d d
    # This is clearly wrong â€” needs to account for unicode
    tag         <- [a-zA-z0-9_]+
    min         <- "min_"
    sep         <- ":"
    integer     <- d d*
    d           <- [0-9]
    __          <- [\s]
