# https://dev.twitter.com/rest/public/search
grammar Query

    # Query Shape Rules

    # One or more operators makes up a query
    query       <- __* (operator __+)* operator __*

    # Operators can be grouped with an OR operator in pairs.
    # I'm not sure if this is the ideal way to do this with PEG
    # but it works for now.
    operator    <- left:orable or right:orable
                 / op

    # Only some oprators work in an OR
    orable      <- exact
                 / filter
                 / reply
                 / engagement
                 / lang
                 / simple

    # If it's not an OR you can use any operator
    op          <- exact
                 / filter
                 / reply
                 / since
                 / until
                 / list
                 / engagement
                 / lang
                 / exclude
                 / question
                 / negation
                 / simple

    simple      <- hashtag
                 / mention
                 / word

    engagement  <- min_rts
                 / min_faves
                 / min_replies

    # Query Syntax Rules

    exact       <- '"' [^\"]* '"'
    filter      <- k:"filter"           sep v:word
    reply       <- k:"to" sep "@"?          v:screen_name
    since       <- k:"since"            sep v:date
    until       <- k:"until"            sep v:date
    list        <- k:"list"             sep v:list_name
    min_rts     <- k:(min "retweets")   sep v:integer
    min_faves   <- k:(min "faves")      sep v:integer
    min_replies <- k:(min "replies")    sep v:integer
    lang        <- k:"lang"             sep v:word
    exclude     <- k:"exclude"          sep v:word
    question    <- "?"
    negation    <- "-" word
    hashtag     <- "#" tag
    mention     <- "@" screen_name
    word        <- [\S]+

    # Utility Rules

    or          <- __+ "OR" __+
    list_name   <- screen_name "/" list_slug
    list_slug   <- [a-z-]+
    screen_name <- [a-zA-z0-9_]+
    # There's probably a better way to do this
    date        <- d d d d "-" d d "-" d d
    # This is clearly wrong â€” needs to account for unicode
    tag         <- [a-zA-z0-9_]+
    min         <- "min_"
    sep         <- ":"
    integer     <- d d*
    d           <- [0-9]
    __          <- [\s]
